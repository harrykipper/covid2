; vaccine hesitancy by age class obtained from: 
; https://yougov.co.uk/topics/health/articles-reports/2021/04/28/vaccine-hesitancy-has-fallen-across-british-societ
to-report getVaccineHesitancy [a]  
  report (ifelse-value 
    a < 25 [0.23]
    a < 50 [0.13]
    a < 65 [0.08]
    [0.04]
  )
end

; Reduction in ability to transmit virus of those vaccinated
to-report getVaccinatedInfectivity
  report (ifelse-value
    vaxed = 1 [0.66]    ; one dose reduces ability to transmit by a third
    vaxed = 2 [0.33]    ; two doses reduce ability to transmit by two thirds
    [1]             ; unvaccinated
  )
end

;; Reduction in probability of infection due to vaccination status of victim. Ex. 1 = no reduction; 0.1 = 90% reduction
to-report getVaccinatedRiskOfInfection [m]  ;; m = mutation being transmitted
  ; show (word "Person with variant " m " met person with " vaxed " doses of vaccine ")
  if m = 3 [
    report (ifelse-value   
      ; the delta variant (m = 3) infects all those with one dose. Full vaccination reduces risk of 50% minus waning.
      vaxed > 1 [1 - (0.5 - immunityWaning)][1]   
    )
  ]
  if m = 4 [
    report (ifelse-value
      vaxed = 2 [riskInfection_2dose + immunityWaning]
      vaxed = 1 [riskInfection_1dose]   
      vaxed > 2 [riskInfection_boosted + immunityWaning]
    )
  ]
  report (ifelse-value
    vaxed = 1 [0.65]                             ;; 35% reduction after first dose
    vaxed > 1 [1 - (0.77 - immunityWaning)]      ;; 77% reduction after second dose, minus waning
    [1]  ; unvaccinated
  )
end

; Risk reduction of developing symptomatic covid
to-report getVaccinatedRiskSymptoms 
  report (ifelse-value
    vaxed = 1 [0.65]
    vaxed > 1 [1 - (0.65 - immunityWaning)]
    [1]  ; unvaccinated
  )
end

to-report getVaccinatedRiskSevere
  report (ifelse-value
    vaxed = 1 [0.65]
    vaxed > 1 [1 - (0.80 - immunityWaning)]
    [1]  ; unvaccinated
  )
end

to-report immunityWaning
  ;; Vaccine-induced immunity starts waning 1 month prior of it completely disappearing 
  let waning days-since-dose - ((immunityLasts * 30) - 30)
  report ifelse-value waning > 0 [waning * 0.01][0]
end

;; Does reducing the interval between doses reduce efficacy? We assume it doesn't
to-report setIntervalReduction    
  report 1
;  report (ifelse-value 
;    dose-interval = 4 [1.6]
;    dose-interval = 8 [1.3]
;    [1]
;  )
end

to receiveVax
  set daily-vaccinations daily-vaccinations - 1  ; global variable
  set vaccinatedToday vaccinatedToday + 1        ; global variable
  set vaxed vaxed + 1                            ; agent variable
  set days-since-dose 0                          ; agent variable
end

to populate-vaccination-table
  ; Source: Scotland vaccination rates by age by sex 9 August 2021
  ; https://public.tableau.com/app/profile/phs.covid.19/viz/COVID-19DailyDashboard_15960160643010/Overview
  set vaccinations table:make
  ;                        age   F-1do F-2do M-1do M-2do
  table:put vaccinations [18 29][0.677 0.291 0.609 0.209]
  table:put vaccinations [30 39][0.725 0.543 0.693 0.478]
  table:put vaccinations [40 49][0.873 0.788 0.895 0.779]
  table:put vaccinations [50 54][0.914 0.866 1 0.935]
  table:put vaccinations [55 59][0.961 0.922 1 1]
  table:put vaccinations [60 64][0.996 0.966 1 1]
  table:put vaccinations [65 69][1 0.993 1 1]
  table:put vaccinations [70 74][0.976 0.956 0.993 0.968]
  table:put vaccinations [75 79][0.967 0.943 0.982 0.955]
  table:put vaccinations [80 100][0.91 0.872 0.922 0.888]
end

to vaccinate-initial-people
  let d 1 
  if Delta_variant != true [
    set d 0.5    ;; If we don't have the Delta variant yet, we are modelling a past situation, with 50% less vaccinated people.
  ]
  show (word "Delta is " Delta_variant "; d = " d)
  foreach table:keys vaccinations [k ->
    let thesepeople turtles with [age >= item 0 k and age <= item 1 k]
    let f thesepeople with [sex = "F"]
    let m thesepeople with [sex = "M"]
    let f1 n-of round ((count f * item 0 table:get vaccinations k) * d) f 
    let f2 n-of round ((count f * item 1 table:get vaccinations k) * d) f1
    let m1 n-of round ((count m * item 2 table:get vaccinations k) * d) m 
    let m2 n-of round ((count m * item 3 table:get vaccinations k) * d) m1
    ;show (word "F: " count f " 1 dose: " count f1 ", 2 doses: " count f2)
    ;show (word "M: " count m " 1 dose: " count m1 ", 2 doses: " count m2)
    ask (turtle-set f1 m1) [
      set vaxed 1
      set novax false
      set days-since-dose random (dose-interval * 7)
    ]
    ask (turtle-set f2 m2) [set vaxed 2]
  ]
end